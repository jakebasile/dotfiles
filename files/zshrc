# Jake Basile's .zshrc

# History

HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# vi mode

bindkey -v

# Options

setopt appendhistory beep extendedglob nomatch
setopt PROMPT_SUBST
setopt CDABLEVARS
setopt CORRECT
setopt GLOBDOTS
setopt HISTIGNORESPACE
setopt HISTIGNOREDUPS
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY

# Functions

function get_branch() {
    git branch --no-color 2> /dev/null | grep '\*' | sed 's/\* \(.*\)/:%{%F{yellow}%}\1%{%f%}/'
}

# this calls tree, ignoring the target directory if it sees a project.clj
function tree() {
    if [[ -a project.clj ]]; then
        /usr/local/bin/tree -phFI target --du $@ | less -FX
    else
        /usr/local/bin/tree -phF --du $@ | less -FX
    fi
}

# these wrap up curl with some default arguments and pretty formatted output.

function cjson() {
  curl -sD /dev/stderr -H "accept: application/json" -H "content-type: application/json" \
    $@ | jq . | less -FX
}

function chtml() {
  curl -sD /dev/stderr -H "accept: text/html" $@ | tidy -ibq -wrap 0 -f /dev/null
}

function cxml() {
  curl -sD /dev/stderr -H "accept: application/xml" $@ | xmllint --format -
}

# Thanks, dogbane of http://unix.stackexchange.com/a/30486
function calc() {
  bc -l <<< "$@"
}

# Completion

autoload -Uz compinit
compinit
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' menu select
zstyle ':completion:*' max-errors 3

# Prompts

PROMPT='%m:%{%F{cyan}%}%1~%{%f%}$(get_branch) %{%F{white}%}%D{%L:%M:%S} %(!.%{%F{red}%}#%{%f%}.%{%F{green}%}$%{%f%}) '
SPROMPT='%{%F{red}%}%R%{%f%} -> %{%F{blue}%}%r%{%f%} (y, n, a, e)'
TMOUT=1
TRAPALRM() {
    zle reset-prompt
}

# Environment

source ~/.path
export EDITOR='vim'
source /usr/local/opt/autoenv/activate.sh

# Aliases

alias ls='ls -lhFG' la='ls -lahFG' # Verbose LS
alias top='sudo htop' # htop > top
alias tup='tmux new -ADs' # Attach session, creating if it doesn't exist.
alias tls='tmux list-sessions'
alias t="echo $TMUX"
alias gpg=gpg2
alias xmlformat="xmllint --format -"
alias pbxml="pbpaste | xmlformat"
alias dinit='$(boot2docker shellinit)'
alias cedn='curl -iH "accept: application/edn" -H "content-type: application/edn"'
alias git=hub

# Misc

bindkey "^R" history-incremental-search-backward
GPG_TTY=$(tty)
export GPG_TTY

