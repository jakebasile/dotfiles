#!/usr/bin/zsh
# Jake Basile's .zshrc

# History

HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# vi mode

bindkey -v

# Options

setopt appendhistory beep extendedglob nomatch
setopt RCQUOTES
setopt NOCLOBBER
setopt PROMPT_SUBST
setopt CORRECT
setopt GLOBDOTS
setopt HISTIGNORESPACE
setopt HISTIGNOREDUPS
setopt INC_APPEND_HISTORY
setopt SHARE_HISTORY

# Functions

function get_branch() {
  git branch --no-color 2> /dev/null | grep '\*' | sed 's/\* \(.*\)/:%{%F{yellow}%}\1%{%f%}/'
}

# this calls tree, ignoring the target directory if it sees a project.clj
function tree() {
  if [[ -a project.clj ]]; then
      env tree -phFI target --du $@ | less -FX
  else
      env tree -phF --du $@ | less -FX
  fi
}

function ppedn() {
  if [[ `uname -s` != "Darwin" ]]; then
    local tmp=$(mktemp -p /tmp "ppedn-XXXXXXXX.clj")
  else
    local tmp=$(mktemp "/tmp/ppedn.clj.XXXXXXXX")
  fi
  echo "(require '[puget.printer :as puget])" >> $tmp
  if [[ ! -z $1 ]]; then
    # if given direct input, use that
    echo "(puget/cprint '$1)" >> $tmp
  else
    # if nothing was given directly, use stdin
    echo "(puget/cprint '$(cat))" >> $tmp
  fi
  lein with-profile ppedn run -m clojure.main $tmp 2> /dev/null
}

# these wrap up curl with some default arguments and pretty formatted output.

function cjson() {
  curl -sD /dev/stderr -H "accept: application/json" -H "content-type: application/json" \
    $@ | jq -C -S . | less -FXr
}

function cedn() {
  curl -sD /dev/stderr -H "accept: application/edn" -H "content-type: application/edn" \
    $@ | ppedn | less -FXr
}

function chtml() {
  curl -sD /dev/stderr -H "accept: text/html" $@ | tidy -ibq -wrap 0 -f /dev/null | less -FXr
}

function cxml() {
  curl -sD /dev/stderr -H "accept: application/xml" $@ | xmllint --format - | less -FXr
}

# Thanks, dogbane of http://unix.stackexchange.com/a/30486
function calc() {
  bc -l <<< "$@"
}

# this makes tmux a bit easier to work with for my usage. with no arguments,
# it lists all open sessions with the current one highlighted (if you are in one).
# With an argument it creates a new session.

function t() {
  if [[ $# = 0 ]]; then
    if [[ -n $TMUX ]]; then
      local this_session=$(tmux display-message -p "#S")
      echo "$this_session (current)"
      tmux list-session -F '#S' | grep -v $this_session
    else
      tmux list-session -F '#S'
    fi
  else
    tmux -2 new-session -ADs $1
  fi
}

# Completion

autoload -Uz compinit
compinit
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' menu select
zstyle ':completion:*' max-errors 3

# Prompts

PROMPT='%m:%{%F{cyan}%}%1~%{%f%}$(get_branch) %{%F{white}%}%D{%L:%M:%S} %(!.%{%F{red}%}#%{%f%}.%{%F{green}%}$%{%f%}) '
SPROMPT='%{%F{red}%}%R%{%f%} -> %{%F{blue}%}%r%{%f%} (y, n, a, e)'
TMOUT=1
TRAPALRM() {
  zle reset-prompt
}

# Environment

source ~/.path

export EDITOR='vim'
export LANG=en_US.UTF-8
export GPG_TTY=`tty`

# Aliases

if [[ `uname -s` != "Darwin" ]]; then
  alias pbcopy='xclip -in -sel clip'
  alias pbpaste='xclip -out -sel clip'
  alias top='htop' # htop > top
else
  alias top='sudo htop'
  alias dock='docker-machine start default; eval "$(docker-machine env default)"'
  alias undock='docker-machine stop default'
fi
alias rm=trash
alias ls='ls -lhFG' la='ls -lahFG' # Verbose LS
alias tls='tmux list-sessions'
alias tcopy='tmux save-buffer - | pbcopy'
alias tpaste='pbpaste | tmux load-buffer -'
alias xmlformat="xmllint --format -"
alias pbxml="pbpaste | xmlformat"
alias jdate='date -ud "+1 year" +"\"%Y-%m-%dT%H:%M:%SZ\""'
alias gpg=gpg2

# Misc

bindkey "^R" history-incremental-search-backward

